package hu.elte.txtuml.examples.train;

import hu.elte.txtuml.api.^model.ModelExecutor
import hu.elte.txtuml.examples.train.xmodel.Backward
import hu.elte.txtuml.examples.train.xmodel.Engine
import hu.elte.txtuml.examples.train.xmodel.Forward
import hu.elte.txtuml.examples.train.xmodel.GE
import hu.elte.txtuml.examples.train.xmodel.GL
import hu.elte.txtuml.examples.train.xmodel.Gearbox
import hu.elte.txtuml.examples.train.xmodel.LE
import hu.elte.txtuml.examples.train.xmodel.Lamp
import hu.elte.txtuml.examples.train.xmodel.Light

execution XTrainTester {
	ModelExecutor.Settings.setExecutorLog(true);
	Gearbox g = create(Gearbox);
	Engine e = create(Engine);
	Lamp l = create(Lamp);
	link(GE.g, g, GE.e, e);
	link(GL.g, g, GL.l, l);
	link(LE.l, l, LE.e, e);
	start(g);
	start(e);
	start(l);

	int time = 50;
	for(int i=0; i<3; i++) {
		Thread.sleep(time);
		log("");
		send new Light() to l;
	}

	Thread.sleep(2 * time);
	
	for(int i=0; i<3; i++) {
		Thread.sleep(3 * time);
		log("");
		send new Forward() to g;

		Thread.sleep(time);
		log("");
		send new Backward() to g;
	}

	ModelExecutor.shutdown();
}
